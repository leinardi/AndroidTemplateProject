/*
 * Copyright 2022 Roberto Leinardi.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'forlago.android-core-conventions'
    id 'com.google.dagger.hilt.android'
    alias(libs.plugins.kotlinx.serialization)
    alias(libs.plugins.apollo)
}

android {
    resourcePrefix 'network_'
    defaultConfig {
        consumerProguardFiles "$projectDir/proguard-network-consumer-rules.pro"
    }
    kotlinOptions {
        freeCompilerArgs += '-Xopt-in=kotlinx.serialization.ExperimentalSerializationApi'
    }
}

apollo {
    packageName = "com.leinardi.forlago.core.network.graphql"
    generateApolloMetadata = true
}

dependencies {
    implementation project(':modules:core-android')
    implementation project(':modules:core-preferences')
    api libs.coroutines.android
    api libs.coroutines.core
    api libs.kotlinx.serialization
    api libs.retrofit
    implementation libs.apollo
    implementation libs.apollo.cache
    implementation libs.apollo.cache.sqlite
    implementation libs.hilt.android
    implementation libs.okhttp3.logging.interceptor
    implementation libs.retrofit.kotlinx.serialization
    implementation libs.timber
    kapt libs.hilt.compiler
    kaptAndroidTest libs.hilt.android.compiler
}

tasks.register("refreshGraphQlSchema", Exec) {
    def endpoint = 'https://apollo-fullstack-tutorial.herokuapp.com/graphql'
    def schemaPath = 'modules/core-network/src/main/graphql/schema.graphqls'
    workingDir rootDir
    commandLine './gradlew', 'downloadApolloSchema', "--endpoint=$endpoint", "--schema=$schemaPath"
}
